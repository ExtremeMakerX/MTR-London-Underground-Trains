import org.apache.tools.ant.filters.ReplaceTokens
import org.mtr.mod.BuildTools

plugins {
	id "fabric-loom" version "1.10.4"
}

final BuildTools buildTools = new BuildTools(minecraftVersion, "fabric", project)

loom {
	runConfigs.configureEach { ideConfigGenerated = true }
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraftVersion}"
	mappings "net.fabricmc:yarn:${buildTools.getYarnVersion()}:v2"
	modImplementation "net.fabricmc:fabric-loader:${buildTools.getFabricVersion()}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${buildTools.getFabricApiVersion()}"
	if (minecraftVersion == "1.16.5") {
		modApi "com.terraformersmc:modmenu:1.16.23"
	} else {
		modApi "com.terraformersmc:modmenu:9.0.0"
	}
	modImplementation "org.mtr:MTR-fabric:${mtrVersion}+${minecraftVersion}-server"
}

repositories {
	maven { url = "https://maven.terraformersmc.com/" }
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(buildTools.javaLanguageVersion))
	}
	withSourcesJar()
	withJavadocJar()
}

tasks.register("setupFiles") {
	copy {
		outputs.upToDateWhen { false }
		from "src/main/fabric.mod.template.json"
		into "src/main/resources"
		filter(ReplaceTokens, tokens: ["minecraft": minecraftVersion, "version": version])
		rename "(.+).template.json", "\$1.json"
	}
}

build {
	doLast {
		buildTools.copyBuildFile()
	}
}
